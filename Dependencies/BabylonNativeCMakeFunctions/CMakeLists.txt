function(target_link_to_dependencies)
set(target ${ARGV0})
math(EXPR lastIdx ${ARGC}-1)
set(scope PUBLIC)
foreach(idx RANGE 1 ${lastIdx})
    set(arg "${ARGV${idx}}")
    if(arg STREQUAL "PUBLIC")
        set(scope PUBLIC)
    elseif(arg STREQUAL "PRIVATE")
        set(scope PRIVATE)
    elseif(arg STREQUAL "INTERFACE")
        set(scope INTERFACE)
    elseif(TARGET ${arg})
        function(on_before_link target)
        endfunction()

        function(on_after_link target)
        endfunction()

        get_target_property(type ${arg} TYPE)
        if(NOT type STREQUAL "INTERFACE_LIBRARY")
            get_target_property(cmakeFile ${arg} ON_LINKED_AS_DEPENDENCY_CMAKE_FILE)
            if(NOT cmakeFile STREQUAL "cmakeFile-NOTFOUND")
                include(${cmakeFile} REQUIRED)
            endif()
        endif()

        on_before_link(${target})
        target_link_libraries(${target} ${scope} ${arg})
        on_after_link(${target})
    else()
        target_link_libraries(${target} ${scope} ${arg})
    endif()
endforeach()
endfunction()